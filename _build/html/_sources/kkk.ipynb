{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# local v: di\n",
    "\n",
    "Lets recap the last 7 weeks in the spirit of a [coda](https://en.wikipedia.org/wiki/Coda_(music)):\n",
    "\n",
    "* `help`\n",
    "   * the help command followed by any command you wish to further explore \n",
    "   * e.g., `h twoway`\n",
    "\n",
    "* `tokenize`\n",
    "   * represent a numerical sequence (e.g. 1,2,3,...,6)  \n",
    "   * as a sequence of letters (e.g. a,b,c,...,z or A,B,C,...,Z)\n",
    "\n",
    "* `quietly`\n",
    "   * how to control what is displayed in the Stata terminal\n",
    "   * Stata uses this e.g. with the neat regression output, and with profile.do\n",
    "   * may very easily be changed to `noisily` if that suits your needs\n",
    "\n",
    "* `delimit`\n",
    "   * neat way to mimic `SAS` in defining the end of **one \"line\" of code**\n",
    "   * especially useful when dealing with graphical output and numerous graphing options\n",
    "   * absolutely necessary if ever you wish to translate `.SAS` code into `.do`\n",
    "   * may make accessible what otherwise were inaccessible datasets (e.g. [NHANES](https://pubmed.ncbi.nlm.nih.gov/?term=nhanes))\n",
    "\n",
    "* `rnormal()`\n",
    "   * simulating parametric distributions\n",
    "   * you may develop this idea on your own in the future\n",
    "   * but we lay the foundation for this developement somewhere below\n",
    "   * `runiform()` & `rnormal()` are the only distributions demonstrated thus far\n",
    "   * `rbinomial()` & `rweibull()` are introduced below in the context of time-to-event data\n",
    "\n",
    "* `pwd`\n",
    "   * pwd\n",
    "   * di c(pwd)\n",
    "   * di \"text with embedded `c(pwd)' for whatever reason\"\n",
    "   * global filepath c(pwd)\n",
    "\n",
    "* `r(mean)`\n",
    "   * creturn list\n",
    "   * return list\n",
    "   * ereturn list\n",
    "\n",
    "* `by`\n",
    "   * collapse (`statistic`) varname1 by(varname2)\n",
    "   * egen varname1=`statistic`(varname2)\n",
    "   * above commands are **equivalent** in many regards\n",
    "   * one distorts the data; the other doesn't do so at all\n",
    "   * useful created twoway plots or other graphical output\n",
    "\n",
    "* `program define`\n",
    "   * rigid, specific programs (without `syntax varlist` or `syntax, options`)\n",
    "   * flexible, generalizable programs (with `syntax [varlist]`, `[options]`) \n",
    "       + [table1_options.ado](https://raw.githubusercontent.com/jhustata/book/main/table1_options.ado)\n",
    "       + [ind_translator.ado](https://raw.githubusercontent.com/jhustata/book/main/ind_translator.ado)\n",
    "       + [table_transplant.do](https://raw.githubusercontent.com/jhustata/book/main/table1_transplant.do)\n",
    "\n",
    "* `capture`\n",
    "   * preventing error results and an arrested script\n",
    "   * Stata is finicky, *pernickety* and this little preface to a command may spare you untold misery\n",
    "   * we've used it most frequently before `program define` & `log using`\n",
    "   * but, notably, we've used it as a parsimonous variant in `syntax varlist if`\n",
    "\n",
    "* `twoway`\n",
    "   * last weeks class, which we didn't cover because of `hw1`-related issues\n",
    "   * notes have been updated to give you the best bang for your buck \n",
    "   * lookout for the following **very** important issues:\n",
    "     * workflow;\n",
    "     * open science; and,\n",
    "     * chatGPTs take on these.\n",
    "\n",
    "* on your own, please go walk step-by-step, command-by-command, through the `twoway` examples offered\n",
    "\n",
    "* think about `if macro {` conditional code-blocks and your deployment of this artifice in future, very powerful Stata scripts!\n",
    "\n",
    "Today we'll circle back to the method emplyed throughout this class:\n",
    "\n",
    "   * defining macros\n",
    "   * formatting them\n",
    "   * [embedding](https://jhustata.github.io/book/ggg.html) them in strings, text, figures, and excel\n",
    "   * producing aesthically pleasing, richly informative output\n",
    "\n",
    "But to what end?\n",
    "\n",
    "   * the advanced Stata Programming class answers that question thus:\n",
    "       * open science\n",
    "       * self-publication\n",
    "       * collaboration\n",
    "       * etc... "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "import networkx as nx\n",
    "import matplotlib.pyplot as plt\n",
    "#import numpy as np\n",
    "#import sklearn as skl\n",
    "# \n",
    "\n",
    "#plt.figure(figsize=[2, 2])\n",
    "G = nx.DiGraph()\n",
    "G.add_node(\"workflow\",  pos = (0,700) )\n",
    "G.add_node(\"jupyter\",  pos = (-2000, 960) )\n",
    "G.add_node(\"dofile\",  pos = (2000, 950) )\n",
    "G.add_node(\"python \", pos = (-3000, 550) )\n",
    "G.add_node(\"stata\", pos = (3000, 550) )\n",
    "G.add_node(\"build\", pos = (-1900, 150) )\n",
    "G.add_node(\"dyndoc\", pos = (1900, 150) )\n",
    "G.add_node(\"html\", pos = (0,0))\n",
    "G.add_node(\"push\", pos = (0, -475))\n",
    "G.add_node(\"ghp\", pos = (0, -950))\n",
    "G.add_edges_from([ (\"jupyter\",\"python \"), (\"dofile\", \"stata\")])\n",
    "G.add_edges_from([(\"python \", \"build\"), (\"stata\", \"dyndoc\") ])\n",
    "G.add_edges_from([(\"build\", \"html\"), (\"dyndoc\", \"html\")])\n",
    "G.add_edges_from([(\"html\",\"push\")])\n",
    "G.add_edges_from([(\"push\",\"ghp\")])\n",
    "nx.draw(G, \n",
    "        nx.get_node_attributes(G, 'pos'), \n",
    "        with_labels=True, \n",
    "        font_weight='bold', \n",
    "        node_size = 4500,\n",
    "        node_color = \"lightblue\",\n",
    "        linewidths = 3)\n",
    "ax= plt.gca()\n",
    "ax.collections[0].set_edgecolor(\"#000000\")\n",
    "ax.set_xlim([-5000, 5000])\n",
    "ax.set_ylim([-1000, 1000])\n",
    "plt.show()"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "If at all you've enjoyed the conveniences brought forth by this classbook, then it would be remiss of me not to let you know from whence this book comes:\n",
    "\n",
    "* a data science [class](https://www.jhsph.edu/courses/course/36630/2022/140.628.41/data-science-for-public-health-i)\n",
    "* python environment\n",
    "* jupyter notebooks\n",
    "* vscode by miscrosoft\n",
    "* dofiles\n",
    "* stata\n",
    "\n",
    "In the [advanced](https://jhustata.github.io/class700/intro.html) [class](https://www.jhsph.edu/courses/course/37447/2022/340.700.71/advanced-stata-programming) we merge these ideas and publish our analyses and documentation (roll over[,](https://www.youtube.com/watch?v=kKlzUKLOm_U) annotation) online via [gh-pages](https://pages.github.com)!!!"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Output\n",
    "\n",
    "```stata\n",
    "local macro: di %3.1f r(mean)*100\n",
    "\n",
    "```\n",
    "\n",
    "* Text\n",
    "     * \"You may embed a `macro` in text\"\n",
    "* Figures\n",
    "     * `line` y x, text(50 18 \"`macro`\")\n",
    "* Excel\n",
    "     * `putexcel` set filename, replace\n",
    "     * putexcel B2=\"`macro`\"\n",
    "\n",
    "* Word\n",
    "     * `putdocx clear`\n",
    "     * `putdocx begin`\n",
    "     * `putdocx` paragraph\n",
    "     * `putdocx` text\n",
    "     * `putdocx save` doc1.docx, replace  \n",
    "\n",
    "* Dataset\n",
    "     * `postfile` regcoeffs b1 b2 b3 using regoutput, replace \n",
    "     * `postfile` regcoeffs () () ()\n",
    "     * `postfile` regcoeffs () () ()\n",
    "     * `postclose` regcoeffs\n",
    "     * `ls *.dta` //look for regoutput.dta in your pwd\n",
    "\n",
    "* Markdown\n",
    "     * `dyndoc`\n",
    "     * html"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### postfile"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "use transplants, clear\n",
    "sum peak_pra,d\n",
    "g highpra=peak_pra>r(p90)\n",
    "sum wait_yrs,d\n",
    "g longwait=wait_yrs>r(p50)\n",
    "postutil clear \n",
    "postfile pp xaxis str80 coef double(result lb ub pvalue) using betas.dta, replace \n",
    "logistic highpra gender prev_ki bmi age wait_yrs don_ecd\n",
    "local xaxis=1\n",
    "qui foreach v of varlist gender prev_ki bmi age wait_yrs don_ecd {  \n",
    "\tlincom `v'\n",
    "\treturn list \n",
    "\tlocal est: di %3.2f r(estimate)\n",
    "\tlocal lb: di %3.2f r(lb)\n",
    "\tlocal ub: di %3.2f r(ub)\n",
    "\tlocal pval: di %3.2f r(p)\n",
    "    post pp (`xaxis') (\"`v'\") (`est') (`lb') (`ub') (`pval')\n",
    "\tlocal xaxis=`xaxis' + 1\n",
    "}\n",
    "\n",
    "postclose pp\n",
    "\n",
    "ls -l\n",
    "use betas,clear\n",
    "list \n",
    "#delimit ;\n",
    "twoway (scatter result xaxis)\n",
    "       (rcap lb ub xaxis, \n",
    "\t       scale(log)\n",
    "\t       yline(1, \n",
    "\t            lc(lime) \n",
    "\t\t\t    lp(dash)\n",
    "\t            )\n",
    "\t       legend(off)\n",
    "\t       xlab(\n",
    "\t           1 \"Gender\" \n",
    "\t           2 \"Previous Tx\"\n",
    "\t           3 \"BMI\"\n",
    "\t\t       4 \"Age\"\n",
    "\t\t       5 \"Wait y\"\n",
    "\t           6 \"ECD\"\n",
    "\t           )\n",
    "\t       ti(\"Association between High PRA and select risk factors\", pos(11))\n",
    "\t       yti(\"OR\", \n",
    "\t           orientation(horizontal)\n",
    "\t\t      )\n",
    "\t       xti(\"\")\n",
    "\t      )\n",
    "        ;\n",
    "#delimit cr\n",
    "graph export logistic.png,replace \n",
    "```\n",
    "\n",
    "![](logistic.png)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### alternative: coefplot\n",
    "\n",
    "```stata\n",
    "\n",
    "use transplants, clear\n",
    "sum peak_pra,d\n",
    "g highpra=peak_pra>r(p90)\n",
    "sum wait_yrs,d\n",
    "g longwait=wait_yrs>r(p50)\n",
    "logistic highpra prev_ki bmi age wait_yrs don_ecd if gender==0\n",
    "est store male\n",
    "logistic highpra prev_ki bmi age wait_yrs don_ecd if gender==1\n",
    "est store female\n",
    "if 0 {\n",
    "\tlab var highpra \"PRA>90th %\"\n",
    "\tlab var longwait \"Wait>50th %\"\n",
    "\tlab var prev_ki \"Previous Tx\"\n",
    "\tlab var bmi \"BMI, kg/m2\"\n",
    "\tlab var age \"Age, y\"\n",
    "\tlab var don_ecd \"ECD\"\n",
    "}\n",
    "coefplot male female, drop(_cons) xline(0) \n",
    "\n",
    "```\n",
    "\n",
    "![](coefplot.png)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A word on **simulation**: \n",
    "\n",
    "* worth developing skills\n",
    "* making up data\n",
    "* imaginary scenarios\n",
    "* and why would one do that?\n",
    "    * missing data\n",
    "    * bootstrap\n",
    "    * cryptography/disclosure risk\n",
    "\t* sample-size calculation (variant on missing data)\n",
    "\t* etc.\n",
    "\n",
    "```stata\n",
    "qui {\n",
    "\tclear \n",
    "\tcls\n",
    "\tif c(N) { //background\n",
    "\t\tinspired by polack et al. nejm 2020\n",
    "\t\tNEJM2020;383:2603-15\n",
    "\t\tlets do some reverse engineering\n",
    "\t\taka simulate, generate data \n",
    "\t\tfrom results: reversed process!!\n",
    "\t}\n",
    "\tif c(os)==\"Windows\" { //methods\n",
    "\t    lobal workdir \"`c(pwd)'\\\"\n",
    "\t}\n",
    "\telse {\n",
    "\t    global workdir \"`c(pwd)'/\"\n",
    "\t}\n",
    "\tcapture log close\n",
    "\tlog using ${workdir}simulation.log, replace \n",
    "\tset seed 340600\n",
    "\tset obs 37706\n",
    "\t}\n",
    "\tif c(N)==37706 { //simulation \n",
    "\t    #delimit ; \n",
    "\t\t//row1\n",
    "\t\tg bnt=rbinomial(1,.5);\n",
    "\t\tlab define Bnt \n",
    "\t\t    0 \"Placebo\"  \n",
    "\t        1 \"BNT162b2\" ;\n",
    "\t\tlabel values bnt Bnt ;\n",
    "\t\ttab bnt ; \n",
    "\t\t//row2\n",
    "\t\tgen female=rbinomial(1, .494); \n",
    "\t\tlabel define Female  \n",
    "\t\t    0 \"Male\"  \n",
    "\t\t\t1 \"Female\"; \n",
    "\t\tlabel values female Female; \n",
    "\t\ttab female;\n",
    "\t\t//row3 \n",
    "\t\ttempvar dem ;\n",
    "\t\tgen `dem'=round(runiform(0,100),.1); \n",
    "\t\trecode `dem'  \n",
    "\t\t    (0/82.9=0)  \n",
    "\t\t    (83.0/92.1=1)  \n",
    "\t\t    (92.2/96.51=2)   \n",
    "\t\t    (96.52/97.0=3)  \n",
    "\t\t    (97.1/97.2=4)  \n",
    "\t\t    (97.3/99.41=5)  \n",
    "\t\t    (99.42/100=6)  \n",
    "\t\t         , gen(race);\n",
    "\t\tlab define Race  \n",
    "\t\t\t0 \"White\"    \n",
    "\t\t    1 \"Black or African American\" \n",
    "\t\t\t2 \"Asian\" \n",
    "\t\t\t3 \"Native American or Alsak Native\"  \n",
    "\t\t\t4 \"Native Hawaiian or other Pacific Islander\"  \n",
    "\t\t\t5 \"Multiracial\"  \n",
    "\t\t\t6 \"Not reported\"; \n",
    "\t\tlabel values race Race; \n",
    "\t\ttab race;\n",
    "\t\t//row4 \n",
    "\t\tgen ethnicity=rbinomial(1,0.28);\n",
    "\t\ttostring ethnicity, replace;\n",
    "\t\treplace ethnicity=\"Latinx\" if ethnicity==\"1\";\n",
    "\t\treplace ethnicity=\"Other\" if ethnicity==\"0\";\n",
    "\t\t//row5 \n",
    "\t\ttempvar country;\n",
    "\t\tgen `country'=round(runiform(0,100), .1);\n",
    "\t\trecode `country'   \n",
    "\t\t    (0/15.3=0)  \n",
    "\t\t\t(15.4/21.5=1)  \n",
    "\t\t\t(21.6/23.6=2)  \n",
    "\t\t\t(23.7/100=3) \n",
    "\t\t\t    , gen(country) ;\n",
    "\t\tlabel define Country \n",
    "\t\t\t0 \"Argentina\"  \n",
    "\t\t    1 \"Brazil\"  \n",
    "\t\t\t2 \"South Africa\"  \n",
    "\t\t\t3 \"United States\"; \n",
    "\t\tlabel values country Country; \n",
    "\t\ttab country;\n",
    "\t\t//row7 \n",
    "\t\tgen age=(rt(_N)*9.25)+52 ; \n",
    "\t\treplace age=runiform(16,91)  \n",
    "\t\t    if !inrange(age,16,91); \n",
    "\t\tsumm age, d ;\n",
    "\t\tlocal age_med=r(p50); local age_lb=r(min); local age_ub=r(max);\n",
    "\t\tgen dob = d(27jul2020) -  \n",
    "\t\t          (age*365.25) ; \n",
    "\t\tgen dor = dob + age*365.25 + runiform(0,4*30.25); \n",
    "\t\t//row6 \n",
    "\t\tgen over55=age>55 ; tab over55;\n",
    "\t\t//row8 \n",
    "\t\tgen bmi=rbinomial(1, .351); tab bmi; \n",
    "\t\t//figure 3 \n",
    "\t\tg days=rweibull(.7,17,0) if bnt==0 ;\n",
    "\t\tg covid=rbinomial(1, 162/21728) if bnt==0 ; \n",
    "\t\treplace days=rweibull(.4,.8,0) if bnt==1 ;\n",
    "\t\treplace covid=rbinomial(1, 14/21772) if bnt==1; \n",
    "\t\t//key dates \n",
    "\t\tgen eft = dor + days;\n",
    "\t\t//date formats\n",
    "\t\tformat dob %td; format dor %td; format eft %td;\n",
    "\t\t //kaplan-meier curve\n",
    "\t\t stset days, fail(covid) ;\n",
    "\t\t sts graph,  \n",
    "\t\t    by(bnt)  \n",
    "\t\t    fail per(100)  \n",
    "\t\t    tmax(119)  \n",
    "\t\t    xlab(0(7)119) \n",
    "\t\t    ylab(0(.4)2.4, \n",
    "\t\t        angle(360)    \n",
    "\t\t\t    format(\"%3.1f\")\n",
    "\t\t\t\t)  \n",
    "\t\t    xti(\"Days after Dose 1\")  \n",
    "\t\t    legend(off) \n",
    "\t\t    text(\n",
    "\t\t\t    2.3 100 \n",
    "\t\t\t    \"Placebo\",  \n",
    "\t\t\t     col(navy)\n",
    "\t\t\t\t )  \n",
    "\t\t    text(\n",
    "\t\t\t    .5 100 \n",
    "\t\t\t\t\"BNT162b2\",  \n",
    "\t\t\t    col(maroon)\n",
    "\t\t\t\t) ;\n",
    "\t\tgraph export BNT162b2.png, replace ;\n",
    "\t\tstcox bnt ;\n",
    "\t\tdrop _* age over55 days ;\n",
    "\t\tg bnt_id=round(runiform(37,37+_N)) ;\n",
    "\t\tcompress  ;\n",
    "\t\t#delimit cr\n",
    "\t\t//label variables\n",
    "\t\tlab var bnt_id \"Participant Identifier\"\n",
    "\t\tlab var bnt \"Random treatment assignment\"\n",
    "\t\tlab var female \"Gender at birth\"\n",
    "\t\tlab var race \"Self-identified race\"\n",
    "\t\tlab var ethnicity \"Hispanic ethnicity\"\n",
    "\t\tlab var country \"Country where trial was conducted\"\n",
    "\t\tlab var dob \"Date of birth\"\n",
    "\t\tlab var dor \"Date of recruitment into BNT162b2 trial\"\n",
    "\t\tlab var eft \"Date of exit from BNT162b2 trial\"\n",
    "\t\tlab var bmi \"Obese\"\n",
    "\t\tlab var covid  \"Covid-19 status on eft date\"\n",
    "\t\t//label data\n",
    "\t\tlab data \"Safety and Efficacy of the BNT162b2 mRNA Covid-19 Vaccine\"\n",
    "\t\tdescribe\n",
    "\t\torder bnt_id dob female race ethnicity country bmi bnt eft covid \n",
    "\t\t*replace eft=. if eft>d(15dec2020) //some folks lost to followup\n",
    "\t\tsave BNT162b2, replace \n",
    "\n",
    "\t}\n",
    "  log close \n",
    "}\t\t\n",
    "```"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Handling **strings** such as drug names: \n",
    "\n",
    "* We'll review a script I wrote 4 years ago\n",
    "* Then compare it with an update I wrote yesterday\n",
    "* In brief, there is a fundamental transformation in:\n",
    "\n",
    "     * aesthetics, legibility, & brevity\n",
    "     * one is 440 lines, repetitious\n",
    "     * the other is 164 lines, utilizes macros & loops     "
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "import excel \"1-first gen antihisitamines.xlsx\", sheet(\"de-duplicated list\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antihist\"\n",
    "keep *_gnn\n",
    "gen group=1\n",
    "save 01_antihistamines, replace\n",
    "\n",
    "import excel \"2-antiparkinsonian agents.xlsx\", sheet(\"De-duplicated list\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antipark\"\n",
    "keep *_gnn\n",
    "gen group=2\n",
    "save 02_antiparkinsons,replace\n",
    "\n",
    "import excel \"3-Antispasmodics.xlsx\", sheet(\"De-duplicated_list\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antispasm\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=3\n",
    "save 03_antispasmodics,replace \n",
    "\n",
    "import excel \"4-Antithrombotics\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antithromb\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=4\n",
    "save 04_antithrombotics,replace\n",
    "\n",
    "import excel \"5-antiinfective agents\", sheet(\"Sheet2\") clear\n",
    "drop in 1/8\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antiinfect\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=5\n",
    "save 05_antiinfective,replace\n",
    "\n",
    "import excel \"6-Peripheral alpha-1 blockers\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"pera1block\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=6\n",
    "save 06_peripheralalpha1blockers,replace\n",
    "\n",
    "import excel \"7-Central alpha-agonists\", sheet(\"de-duplicated_list\") clear\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"cena1ag\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=7\n",
    "save 07_centralalpha1agonists,replace \n",
    "\n",
    "import excel \"8-Other CNS alpha-agonists\", sheet(\"De-duplicated_list\") clear\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"otha1ag\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=8\n",
    "save 08_otheralpha1agonists,replace \n",
    "\n",
    "import excel \"9-Antidepressants\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/8\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antidep\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=9\n",
    "save 09_antidepressants,replace \n",
    "\n",
    "import excel \"10-Antipsychotic agents\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/8\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antipsy\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=10\n",
    "save 10_antipsychotics,replace \n",
    "\n",
    "import excel \"11-Barbituates\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"barb\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=11\n",
    "save 11_barbiturates,replace \n",
    "\n",
    "import excel \"12-Benzodiazepine lists\", sheet(\"Short-acting\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"benzoshort\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "tempfile benzoshort\n",
    "save `benzoshort',replace\n",
    "\n",
    "import excel \"12-Benzodiazepine lists\", sheet(\"Long-acting\") clear\n",
    "drop in 1/8\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"benzolong\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "tempfile benzolong\n",
    "save `benzolong',replace\n",
    "\n",
    "import excel \"12-Benzodiazepine lists\", sheet(\"Unknown\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"benzounk\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "tempfile benzounk\n",
    "save `benzounk',replace\n",
    "         use `benzoshort',clear\n",
    "append using `benzolong'\n",
    "append using `benzounk'\n",
    "gen group=12\n",
    "save 12_benzodiazepines,replace \n",
    "\n",
    "import excel \"13-Nonbenzodiazepines - Z-drugs\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"nonbenzo\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=13\n",
    "save 13_nonbenzodiazepines,replace \n",
    "\n",
    "import excel \"14-Ergoloid Mesylates\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"ergot\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=14\n",
    "save 14_ergoloids,replace \n",
    "\n",
    "import excel \"15-Androgens\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"andro\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=15\n",
    "save 15_androgens,replace \n",
    "\n",
    "import excel \"16-Estrogens\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/6\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"estro\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=16\n",
    "save 16_estrogens,replace \n",
    "\n",
    "import excel \"17-Growth hormone\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/7\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"growth\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=17\n",
    "save 17_growthhormones,replace \n",
    "\n",
    "import excel \"18-Insulin\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/8\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"insulin\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=18\n",
    "save 18_insulin,replace \n",
    "\n",
    "import excel \"19-Sulfonylureas\", sheet(\"De-duplicated_results\") clear\n",
    "drop in 1/5\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"urea\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=19\n",
    "save 19_sulfonylureas,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"20.Proton_Pump_Inhibitors\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"ppi\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=20\n",
    "save 20_protonpumpinh,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"21.non-selective NSAIDS\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"nsnsaids\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=21\n",
    "save 21_nonselectnsaids,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"22.Skeletal muscle relaxants\") clear\n",
    "drop in 1/2\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"muscle\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=22\n",
    "save 22_musclerelaxants,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"23.non-DHP CCB\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"ccb\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=23\n",
    "save 23_nondhpccb,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"24.Thiazolidinediones\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"thiazo\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=24\n",
    "save 24_thiazolidinediones,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"25.Acetyl cholinesterase inhib\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"achblock\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=25\n",
    "save 25_acetylcholinesteraseinh,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"26.alpha-1 blockers\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"a1block\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=26\n",
    "save 26_alpha1blockers,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"27.ti-cyclic antidepressants\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"tca\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=27\n",
    "save 27_tca,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"28.corticosteroids\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"steroids\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=28\n",
    "save 28_corticosteroids,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"29.H2 receptor antagonists\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"h2ant\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=29\n",
    "save 29_h2rblockers,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"30.antiepileptics\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antiepi\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=30\n",
    "save 30_antiepileptics,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"31.antiemetics\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antieme\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=31\n",
    "save 31_antiemetics,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"32.NSAIDS\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn \n",
    "gen class_gnn=\"nsaid\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=32\n",
    "save 32_nsaids,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"33.Diuretics\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"diuretics\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=33\n",
    "save 33_diuretics,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"34.SNRIs\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"snri\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=34\n",
    "save 34_snri,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"35.SSRIs\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"ssri\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=35\n",
    "save 35_ssri,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"36.RAS Inhibitor\") clear\n",
    "drop in 1/2\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"ras\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=36\n",
    "save 36_rasinhibitors,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"37.Opioids\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"opioids\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=37\n",
    "save 37_opioids,replace \n",
    "\n",
    "import excel \"20-38\", sheet(\"38.Anticholinergic\") clear\n",
    "drop in 1/1\n",
    "replace A = strtrim(A)\n",
    "replace A=ustrupper(A)\n",
    "rename A drug_gnn\n",
    "gen class_gnn=\"antich\"\n",
    "keep *_gnn\n",
    "keep if !missing(drug)\n",
    "gen group=38\n",
    "save 38_anticholinergics,replace \n",
    "```"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "\n",
    "qui {\n",
    "\tqui {\n",
    "\t\tclear\n",
    "\t\tcls\n",
    "\t\tif c(N) { //background\n",
    "\t\t\twe compiled a comprehensive list of medications within \n",
    "\t\t\teach of the potentially inapporpriate medication (pim) \n",
    "\t\t\tclasses in a systematic manner.\n",
    "\t\t\t\n",
    "\t\t\tfirst, informaticists used micromedex, the control vocabularies of            medline and embase, and medication websites to generate \n",
    "\t\t\ta trade and generic medication name list. \n",
    "\t\t\t\n",
    "\t\t\tsecond, this list was curated to allow medications with multiple\n",
    "            mechanisms of action to be represented in more than one pim class.\n",
    "\t\t\twe removed pims with topical or ocular routes of administration. \n",
    "\t\t\t\n",
    "\t\t\tthe final list was **imported into stata** to query \n",
    "\t\t\tmedicare part d claims for pims...\n",
    "\t\t} \n",
    "\t\tif c(N)<1 { //methods\n",
    "\t\t    global workdir `c(pwd)' \n",
    "\t\t\tif c(os)==\"Windows\" {\n",
    "\t\t\t\tglobal workdir \"$workdir\\\"\n",
    "\t\t\t}\n",
    "\t\t\telse {\n",
    "\t\t\t\tglobal workdir \"$workdir/\"\n",
    "\t\t\t}\n",
    "\t\t\t#delimit ;\n",
    "\t\t\tglobal catalog1\n",
    "\t\t\t    \"1-first gen antihisitamines\"\n",
    "\t\t\t\t\"2-antiparkinsonian agents\"\n",
    "\t\t\t\t\"3-Antispasmodics\"\n",
    "\t\t\t\t\"4-Antithrombotics\"\n",
    "\t\t\t\t\"5-antiinfective agents\"\n",
    "\t\t\t\t\"6-Peripheral alpha-1 blockers\"\n",
    "\t\t\t\t\"7-Central alpha-agonists\"\n",
    "\t\t\t\t\"8-Other CNS alpha-agonists\"\n",
    "\t\t\t\t\"9-Antidepressants\"\n",
    "\t\t\t\t\"10-Antipsychotic agents\"\n",
    "\t\t\t\t\"11-Barbituates\"\n",
    "\t\t\t\t\"12-Benzodiazepine lists\"\n",
    "\t\t\t\t\"12-Benzodiazepine lists\"\n",
    "\t\t\t\t\"12-Benzodiazepine lists\"\n",
    "\t\t\t\t\"13-Nonbenzodiazepines - Z-drugs\"\n",
    "\t\t\t\t\"14-Ergoloid Mesylates\"\n",
    "\t\t\t\t\"15-Androgens\"\n",
    "\t\t\t\t\"16-Estrogens\"\n",
    "\t\t\t\t\"17-Growth hormone\"\n",
    "\t\t\t\t\"18-Insulin\"\n",
    "\t\t\t\t\"19-Sulfonylureas\"\n",
    "\t\t\t\t;  \n",
    "\t\t\tglobal catalog2\n",
    "\t\t\t    \"20.Proton_Pump_Inhibitors\"\n",
    "\t\t\t\t\"21.non-selective NSAIDS\"\n",
    "\t\t\t\t\"22.Skeletal muscle relaxants\"\n",
    "\t\t\t\t\"23.non-DHP CCB\"\n",
    "\t\t\t\t\"24.Thiazolidinediones\"\n",
    "\t\t\t\t\"25.Acetyl cholinesterase inhib\"\n",
    "\t\t\t\t\"26.alpha-1 blockers\"\n",
    "\t\t\t\t\"27.ti-cyclic antidepressants\"\n",
    "\t\t\t\t\"28.corticosteroids\"\n",
    "\t\t\t\t\"29.H2 receptor antagonists\"\n",
    "\t\t\t\t\"30.antiepileptics\"\n",
    "\t\t\t\t\"31.antiemetics\"\n",
    "\t\t\t\t\"32.NSAIDS\"\n",
    "\t\t\t\t\"33.Diuretics\"\n",
    "\t\t\t\t\"34.SNRIs\"\n",
    "\t\t\t\t\"35.SSRIs\"\n",
    "\t\t\t\t\"36.RAS Inhibitor\"\n",
    "\t\t\t\t\"37.Opioids\"\n",
    "\t\t\t\t\"38.Anticholinergic\";\n",
    "\t\t\tglobal namelist1\n",
    "\t\t\t    01_antihistamines\n",
    "\t\t\t\t02_antiparkinsons\n",
    "\t\t\t\t03_antispasmodics\n",
    "\t\t\t\t04_antithrombotics\n",
    "\t\t\t\t05_antiinfective\n",
    "\t\t\t\t06_peripheralalpha1blockers\n",
    "\t\t\t\t07_centralalpha1agonists\n",
    "\t\t\t\t08_otheralpha1agonists\n",
    "\t\t\t\t09_antidepressants\n",
    "\t\t\t\t10_antipsychotics\n",
    "\t\t\t\t11_barbiturates\n",
    "\t\t\t    benzoshort \n",
    "\t\t\t\tbenzolong\n",
    "\t\t\t\tbenzounk \n",
    "\t\t\t\t13_nonbenzodiazepines\n",
    "\t\t\t\t14_ergoloids\n",
    "\t\t\t\t15_androgens\n",
    "\t\t\t\t16_estrogens\n",
    "\t\t\t\t17_growthhormones\n",
    "\t\t\t\t18_insulin\n",
    "\t\t\t\t19_sulfonylureas;\n",
    "\t\t\tglobal namelist2\n",
    "\t\t\t\t20_protonpumpinh\n",
    "\t\t\t\t21_nonselectnsaids\n",
    "\t\t\t\t22_musclerelaxants\n",
    "\t\t\t\t23_nondhpccb\n",
    "\t\t\t\t24_thiazolidinediones\n",
    "\t\t\t\t25_acetylcholinesteraseinh\n",
    "\t\t\t\t26_alpha1blockers\n",
    "\t\t\t\t27_tca\n",
    "\t\t\t\t28_corticosteroids\n",
    "\t\t\t\t29_h2rblockers\n",
    "\t\t\t\t30_antiepileptics\n",
    "\t\t\t\t31_antiemetics\n",
    "\t\t\t\t32_nsaids\n",
    "\t\t\t\t33_diuretics\n",
    "\t\t\t\t34_snri\n",
    "\t\t\t\t35_ssri\n",
    "\t\t\t\t36_rasinhibitors\n",
    "\t\t\t\t37_opioids\n",
    "\t\t\t\t38_anticholinergics;\n",
    "\t\t\t#delimit cr \n",
    "\t\t\tcapture log close \n",
    "\t\t\tlog using \"${workdir}01_bc050523.log\",replace \n",
    "\t\t\tset max_memory .\n",
    "\t\t}\n",
    "\t\tif c(N)==0 { //results\n",
    "\t\t    local group=1\n",
    "\t\t\tforeach class in \"$catalog1\" {  \n",
    "\t\t\t    import excel \"${workdir}`class'.xlsx\", clear\n",
    "\t\t\t\tdrop in 1/6\n",
    "\t\t\t\treplace A=strtrim(A)\n",
    "\t\t\t\treplace A=ustrupper(A)\n",
    "\t\t\t\trename A drug_gnn\n",
    "\t\t\t\tlocal class_tidy: di word(\"$namelist1\",`group')\n",
    "\t\t\t\tg class_gnn=\"`class_tidy'\"\n",
    "\t\t\t\tcapture split class_gnn,p(\"_\")\n",
    "\t\t\t\tcapture keep drug_gnn class_gnn2\n",
    "\t\t\t\tif inrange(`group',1,11) {\n",
    "\t\t\t\t\tg group=`group'\n",
    "\t\t\t\t}\n",
    "\t\t\t\telse {\n",
    "\t\t\t\t\tg group=`group'-2\n",
    "\t\t\t\t}\n",
    "\t\t\t\tkeep if !missing(drug)\n",
    "\t\t\t\tlocal filename: di word(\"$namelist1\",`group')\n",
    "\t\t\t\tcapture save \"`filename'\",replace  \n",
    "\t\t\t\tnoi di \"`filename'\"\n",
    "\t\t\t\tlocal group=`group'+1 \n",
    "\t\t\t}\n",
    "\t\t\tuse benzoshort,clear\n",
    "\t\t\tappend using benzolong\n",
    "\t\t\tappend using benzounk //debug\n",
    "\t\t\treplace group=12\n",
    "\t\t\tdrop class_gnn\n",
    "\t\t\trename class_gnn1 class_gnn2\n",
    "\t\t\tsave 12_benzodiazepines,replace \n",
    "\t\t}\n",
    "\t\tclear \n",
    "\t\tif c(N)==0 {\n",
    "\t\t\tlocal group=1\n",
    "\t\t\tforeach class in \"$catalog2\" {\n",
    "\t\t\t\timport excel \"${workdir}20-38.xlsx\", /*\n",
    "\t\t\t\t    */ sheet(\"`class'\") clear \n",
    "\t\t\t\tdrop in 1/1\n",
    "\t\t\t\treplace A=strtrim(A)\n",
    "\t\t\t\treplace A=ustrupper(A)\n",
    "\t\t\t\trename A drug_gnn\n",
    "\t\t\t\tlocal class_tidy: di word(\"$namelist2\",`group')\n",
    "\t\t\t\tg class_gnn=\"`class_tidy'\"\n",
    "\t\t\t\tcapture split class_gnn,p(\"_\")\n",
    "\t\t\t\tcapture keep drug_gnn class_gnn2\n",
    "\t\t\t\tg group=`group'+19\n",
    "\t\t\t\tkeep if !missing(drug)\n",
    "\t\t\t\tlocal filename: di word(\"$namelist2\",`group')\n",
    "\t\t\t\tcapture save \"`filename'\",replace \n",
    "\t\t\t\tnoi di \"`filename'\"\n",
    "\t\t\t\tlocal group=`group'+1\n",
    "\t\t\t\t\n",
    "\t\t\t}\n",
    "\t\t}\n",
    "\t  cls\n",
    "\t  rm benzoshort.dta \n",
    "\t  rm benzolong.dta\n",
    "\t  rm benzounk.dta \n",
    "      noi ls *.dta\n",
    "\t  timer list \n",
    "\t  log close \n",
    "\t}\n",
    "}\n",
    "\n",
    "\n",
    "\t  \n",
    "\n",
    "\t  \n",
    "\n",
    "```\n",
    "\n",
    "Download these .xlsx files into the `pwd` for the above scripts to work:\n",
    "\n",
    "[1-first gen antihisitamines](https://www.dropbox.com/s/gy5ynqzvau6inl4/1-first%20gen%20antihisitamines.xlsx?dl=0)\n",
    "\n",
    "[2-antiparkinsonian agents](https://www.dropbox.com/s/4dvquqf597egvbv/2-antiparkinsonian%20agents.xlsx?dl=0)\n",
    "\n",
    "[3-Antispasmodics](https://www.dropbox.com/s/frejy31ceaenhsz/3-Antispasmodics.xlsx?dl=0)\n",
    "\n",
    "[4-Antithrombotics](https://www.dropbox.com/s/dmu6zoicdkpbb8n/4-Antithrombotics.xlsx?dl=0)\n",
    "\n",
    "[5-antiinfective agents](https://www.dropbox.com/s/9hrngpqbgc956i2/5-antiinfective%20agents.xlsx?dl=0)\n",
    "\n",
    "[6-Peripheral alpha-1 blockers](https://www.dropbox.com/s/nfg25jjtpfpzffs/6-Peripheral%20alpha-1%20blockers.xlsx?dl=0)\n",
    "\n",
    "[7-Central alpha-agonists](https://www.dropbox.com/s/23p8g6fs7vmqpyz/7-Central%20alpha-agonists.xlsx?dl=0)\n",
    "\n",
    "[8-Other CNS alpha-agonists](https://www.dropbox.com/s/0f9xl0umthep7vb/8-Other%20CNS%20alpha-agonists.xlsx?dl=0)\n",
    "\n",
    "[9-Antidepressants](https://www.dropbox.com/s/gajotmuprtmnk1i/9-Antidepressants.xlsx?dl=0)\n",
    "\n",
    "[10-Antipsychotic agents](https://www.dropbox.com/s/ppp99392qhau2zx/10-Antipsychotic%20agents.xlsx?dl=0)\n",
    "\n",
    "[11-Barbituates](https://www.dropbox.com/s/af5ib26ce0cn8tf/11-Barbituates.xlsx?dl=0)\n",
    "\n",
    "[12-Benzodiazepine lists](https://www.dropbox.com/s/jaaxpfma98spimi/12-Benzodiazepine%20lists.xlsx?dl=0)\n",
    "\n",
    "[13-Nonbenzodiazepines - Z-drugs](https://www.dropbox.com/s/cvvzkcilaq487c6/13-Nonbenzodiazepines%20-%20Z-drugs.xlsx?dl=0)\n",
    "\n",
    "[14-Ergoloid Mesylates](https://www.dropbox.com/s/s08u88h7mcx6uof/14-Ergoloid%20Mesylates.xlsx?dl=0)\n",
    "\n",
    "[15-Androgens](https://www.dropbox.com/s/m47qup81r7n8w7z/15-Androgens.xlsx?dl=0)\n",
    "\n",
    "[16-Estrogens](https://www.dropbox.com/s/13o81mficb5abgb/16-Estrogens.xlsx?dl=0)\n",
    "\n",
    "[17-Growth hormone](https://www.dropbox.com/s/bk9p1cb16cutz6u/17-Growth%20hormone.xlsx?dl=0)\n",
    "\n",
    "[18-Insulin](https://www.dropbox.com/s/cxjwtyho9w07zbw/18-Insulin.xlsx?dl=0)\n",
    "\n",
    "[19-Sulfonylureas](https://www.dropbox.com/s/9dttou8zopdefkv/19-Sulfonylureas.xlsx?dl=0)\n",
    "\n",
    "[20-38](https://www.dropbox.com/s/la3708gryt3jxiy/20-38.xlsx?dl=0)\n"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "commands:\n",
    "+ `strpos` \n",
    "+ `word`\n",
    "+ `strlen`\n",
    "+ `regexm`"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### strpos"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "\n",
    "di \"$S_TIME\"\n",
    "clear\n",
    "set more off\n",
    "tempfile pde_2013 pde_2014 pde2013bc pde2014bc \n",
    "use usrds_id srvc_dt gnn using \"/dcs01/igm/segevlab/data/usrds2015/claims/pd/pde2013\",clear\n",
    "save `pde_2013',replace\n",
    "use usrds_id srvc_dt gnn using \"/dcs01/igm/segevlab/data/usrds2016/claims/pd/pde2014\",clear\n",
    "save `pde_2014',replace\n",
    "local year \"y=2013/2014\"\n",
    "forvalues `year' {\n",
    "  capture use `pde_`y'',clear\n",
    "\tif _rc==0 {\n",
    "\t\tdi \"processing pde_`y'.dta ...\"\n",
    "        gen\tantihist16=(strpos(gnn,\"BROMPHENIRAMINE\")!=0)\n",
    "        gen\tantihist19=(strpos(gnn,\"CARBINOXAMINE\")!=0)\n",
    "        gen\tantihist26=(strpos(gnn,\"CHLORPHENIRAMINE\")!=0)\n",
    "        gen\tantihist31=(strpos(gnn,\"CLEMASTINE\")!=0)\n",
    "        gen\tantihist36=(strpos(gnn,\"CYPROHEPTADINE\")!=0)\n",
    "        gen\tantihist39=(strpos(gnn,\"DEXBROMPHENIRAMINE\")!=0)\n",
    "        gen\tantihist40=(strpos(gnn,\"DEXCHLORPHENIRAMINE\")!=0)\n",
    "        gen\tantihist43=(strpos(gnn,\"DIMENHYDRINATE\")!=0)\n",
    "        gen\tantihist47=(strpos(gnn,\"DIPHENHYDRAMINE\")!=0)\n",
    "        gen\tantihist51=(strpos(gnn,\"DOXYLAMINE\")!=0)\n",
    "        gen\tantihist65=(strpos(gnn,\"HYDROXYZINE\")!=0)\n",
    "        gen\tantihist77=(strpos(gnn,\"MECLIZINE\")!=0)\n",
    "        gen\tantihist101=(strpos(gnn,\"PROMETHAZINE\")!=0)\n",
    "        gen\tantihist105=(strpos(gnn,\"PYRILAMINE\")!=0)\n",
    "        gen\tantihist127=(strpos(gnn,\"TRIPROLIDINE\")!=0)      \n",
    "\t\tgen  antihist_rx=( ///\t\n",
    "        antihist16+\t///\n",
    "        antihist19+\t///\n",
    "        antihist26+\t///\n",
    "        antihist31+\t///\n",
    "        antihist36+\t///\n",
    "        antihist39+\t///\n",
    "        antihist40+\t///\n",
    "        antihist43+\t///\n",
    "        antihist47+\t///\n",
    "        antihist51+\t///\n",
    "        antihist65+\t///\n",
    "        antihist77+\t///\n",
    "        antihist101+\t///\n",
    "        antihist105+\t///\n",
    "        antihist127\t///\n",
    "        >0)\n",
    "        keep usrds_id srvc_dt antihist_rx \n",
    "        duplicates drop\n",
    "        quietly compress\n",
    "        save `pde`y'bc', replace  \n",
    "\t}\n",
    "}\n",
    "\n",
    "forvalues `year' {\n",
    "\t\t capture append using `pde`y'bc'\n",
    "}\n",
    "save 01_antihistamines_expR.dta,replace\n",
    "di \"$S_TIME\"        \n",
    "```"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### others\n",
    "\n",
    "```stata\n",
    "\n",
    "//explore these commands sequentially, on your own\n",
    "use transplants, clear\n",
    "list extended_dgn in 1/5, clean\n",
    "disp word(\"Hello, is there anybody in there?\",4)\n",
    "list extended_dgn if word(ext, 5) != \"\", clean noobs\n",
    "disp strlen(\"Same as it ever was\")\n",
    "list extended_dgn if strlen(ext)< 6, clean\n",
    "assert regexm(\"Earth\", \"art\")\n",
    "assert !regexm(\"team\", \"I\")\n",
    "tab ext if regexm(ext, \"HTN\")\n",
    "list ext if regexm(ext, \"^A\")\n",
    "//starts with A\n",
    "list ext if regexm(ext, \"X$\")\n",
    "//ends with X\n",
    "tab ext if regexm(ext, \"HIV.*Y\")\n",
    "//contains \"HIV\", then some otherstuff, then Y\n",
    "```"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Dates"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "disp %td 19400 11feb2013\n",
    "disp %td 366 01jan1961\n",
    "disp %td -5 27dec1959\n",
    "use transplants, clear\n",
    "gen oneweek = transplant_date+7\n",
    "list transplant_date oneweek in 1/3\n",
    "format %td oneweek\n",
    "list transplant_date oneweek in 1/3\n",
    "disp td(04jul1976)\n",
    "disp td(5may2021)\n",
    "disp mdy(7,4,1976)\n",
    "disp mdy(5,5,2021)\n",
    "disp date(\"August 15, 1969\", \"MDY\")\n",
    "disp \"$S_DATE\"\n",
    "di c(current_date)\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.2"
  },
  "widgets": {
   "application/vnd.jupyter.widget-state+json": {
    "state": {},
    "version_major": 2,
    "version_minor": 0
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
